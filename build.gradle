buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:6.0.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${mod_version}-${getDate()}"
group = "goblinbob.mobends"
archivesBaseName = "MoBends_${minecraft_version}"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net' }
}

minecraft {
    mappings channel: "official", version: "1.20.1"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    testImplementation "junit:junit:4.11"
}

jar {
    manifest {
        attributes(
            'Specification-Title': project.name,
            'Specification-Vendor': 'GoblinBob',
            'Built-By': 'GoblinBob',
            'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'FMLAT': 'accesstransformer.cfg',
            'Implementation-Vendor': 'GoblinBob',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        )
    }
}

jar.finalizedBy('reobfJar')

processResources {
    inputs.property "version", mod_version
    inputs.property "mcversion", minecraft_version

    from(sourceSets.main.resources.srcDirs)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

def getDate() {
    return new Date().format('dd.MM.yy')
}
